plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdkVersion 32

    defaultConfig {
        applicationId "no.nordicsemi.android.nrfei"
        minSdkVersion 23
        targetSdkVersion 32
        versionCode 32
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
        // This is required so that Result<T> can be used as return type.
        // http://rustyrazorblade.com/post/2018/2018-12-06-kotlin-result/
        freeCompilerArgs += "-Xallow-result-return-type"
        // This is required to use @OptIn annotation without a warning.
        // https://kotlinlang.org/docs/opt-in-requirements.html#require-opt-in-for-api
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }
    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    namespace 'no.nordicsemi.android.ei'
}

dependencies {

    // https://developer.android.com/jetpack/androidx/releases/core
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.core:core-splashscreen:1.0.0-beta02'
    //implementation 'androidx.appcompat:appcompat:1.3.0'
    // https://github.com/material-components/material-components-android/releases
    implementation 'com.google.android.material:material:1.6.0-beta01'

    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    // https://developer.android.com/jetpack/androidx/releases/lifecycle
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    // https://developer.android.com/jetpack/androidx/releases/activity
    implementation 'androidx.activity:activity-compose:1.4.0'
    // https://github.com/Kotlin/kotlinx.coroutines/releases
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0'
    // https://developer.android.com/jetpack/androidx/releases/navigation
    // Do no upgrade to 2.5.0-alpha versions specifically alpha02/03 for a known issue see below.
    // https://stackoverflow.com/questions/71361999/jetpack-compose-bottomnavigation-java-lang-
    // illegalstateexception-already-atta
    implementation 'androidx.navigation:navigation-compose:2.4.1'
    implementation 'io.coil-kt:coil-compose:2.0.0-rc02'

    // https://kotlin.github.io/kotlinx.coroutines/kotlinx-coroutines-test/
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.0'
    // Paging 3 - https://developer.android.com/jetpack/androidx/releases/paging
    implementation 'androidx.paging:paging-compose:1.0.0-alpha14'

    // BLE Library - https://github.com/NordicSemiconductor/Android-BLE-Library/releases
    implementation 'no.nordicsemi.android:ble-ktx:2.4.0'
    // MCU Manager Library - https://github.com/NordicSemiconductor/Android-nRF-Connect-Device-Manager
    implementation 'no.nordicsemi.android:mcumgr-ble:1.2.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation "com.google.truth:truth:1.1.3"

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // Hilt - Dagger
    implementation "com.google.dagger:hilt-android:$hilt_dagger_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_dagger_version"
    implementation "androidx.hilt:hilt-navigation:$hilt_android_version"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    // For instrumentation tests
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_dagger_version"
    kaptAndroidTest "com.google.dagger:hilt-compiler:$hilt_dagger_version"

    // For local unit tests
    testImplementation "com.google.dagger:hilt-android-testing:$hilt_dagger_version"
    kaptTest "com.google.dagger:hilt-compiler:$hilt_dagger_version"

    // Retrofit - https://github.com/square/retrofit/releases
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.6'
    // https://square.github.io/okhttp/changelog/
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.6'
    // https://github.com/google/gson/releases
    implementation 'com.google.code.gson:gson:2.9.0'

    // So google decided to remove coil from accompanist version 0.16.0 onwards
    // https://coil-kt.github.io/coil/compose/#migrating-from-accompanist
    implementation("io.coil-kt:coil-compose:1.3.2")

    implementation 'com.google.accompanist:accompanist-systemuicontroller:0.20.0'
    implementation "com.google.accompanist:accompanist-permissions:$accompanist_version"
    implementation "com.google.accompanist:accompanist-swiperefresh:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager-indicators:$accompanist_version"
    implementation('org.jetbrains.kotlinx:kotlinx-datetime:0.3.2')

}

kapt {
    correctErrorTypes true
}